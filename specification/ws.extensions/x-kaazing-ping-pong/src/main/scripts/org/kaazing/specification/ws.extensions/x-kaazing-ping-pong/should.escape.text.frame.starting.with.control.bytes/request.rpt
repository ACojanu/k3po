property key ${ws:handshakeKey()}
property writeMask ${ws:randomBytes(4)}

connect http://localhost:8001/echo
connected

write method "GET"
write version "HTTP/1.1"
write header host
write header "Upgrade" "websocket"
write header "Connection" "Upgrade"
write header "Sec-WebSocket-Key" ${key}
write header "Sec-WebSocket-Version" "13"
write header "Sec-WebSocket-Extensions" "x-kaazing-ping-pong"

read status "101" /.+/
read version "HTTP/1.1"
read header "Upgrade" /(?i:websocket)/
read header "Connection" /(?i:Upgrade)/
read header "Sec-WebSocket-Accept" ${ws:handshakeHash(key)}
read header "Sec-WebSocket-Extensions" /x-kaazing-ping-pong; *(?<controlBytes>[a-fA-F0-9]{8})/

# Send a text message starting with the control bytes and followed by "ABC" 
write [0x81 0x84] ${writeMask}
write option mask ${writeMask}
write ${pp:parseHexString(controlBytes)}
write option mask [0x00 0x00 0x00 0x00]

write [0x81 0x87] ${writeMask}
write option mask ${writeMask}
write ${pp:parseHexString(controlBytes)}
write [0x41 0x42 0x43]
write option mask [0x00 0x00 0x00 0x00]

# Expect echoed response, starting with the escape frame
read [0x81 0x04]
read ${pp:parseHexString(controlBytes)}

read [0x81 0x07]
read ${pp:parseHexString(controlBytes)}

read "ABC"

# Do clean close
write [0x88 0x80] ${writeMask}
read [0x88 0x00]

closed
