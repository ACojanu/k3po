property sequence ${wse:randomInt(100)}

connect http://localhost:8080/path/;e/cb?query
connected

write method "POST"
write version "HTTP/1.1"
write header host
write header content-length
write header "X-WebSocket-Version" "wseb-1.1"
write header "X-Accept-Commands" "ping"
write header "X-Sequence-No" ${wse:asString(sequence)}
write close

read status "201" /.+/
read version "HTTP/1.1"
read header "X-WebSocket-Version" "wseb-1.1"
read header "Content-Type" "text/plain;charset=utf-8"

read /(?<upstream>http:\/\/localhost:8080\/path\/.+)/ "\n"
read /(?<downstream>http:\/\/localhost:8080\/path\/.+)/ "\n"
read notify CREATED
read closed

# Downstream
connect await CREATED
connect ${downstream}
connected

write await CREATED
write method "GET"
write version "HTTP/1.1"
write header host
write header "X-Sequence-No" ${wse:asString(sequence + 1)}
write header content-length
write close

read status "200" /.+/
read header "Content-Type" "application/octet-stream"
read header "Connection" "close"

read [0x8A 0x00]

read notify FIRST_PONG_RECEIVED

read closed


# Upstream
connect await CREATED
connect ${upstream}
connected

write await CREATED
write method "POST"
write version "HTTP/1.1"
write header host
write header "Content-Type" "application/octet-stream"
write header "X-Sequence-No" ${wse:asString(sequence + 1)}
write header content-length

write [0x89 0x00]
write [0x01 0x30 0x31 0xFF]
write close

read status "200" /.+/
read version "HTTP/1.1"
read closed

# Out of Order Upstream
connect await CREATED
connect ${upstream}
connected

write await FIRST_PONG_RECEIVED
write method "POST"
write version "HTTP/1.1"
write header host
write header "Content-Type" "application/octet-stream"
write header "X-Sequence-No" ${wse:asString(sequence + 3)}
write header content-length

write [0x89 0x00]
write [0x01 0x30 0x31 0xFF]
write close

read status /4\d\d/ /.+/
read closed
