property client256 ${wse:randomBytes(256)}

connect http://localhost:8080/path/;e/cb?query
connected

write method "POST"
write version "HTTP/1.1"
write header host
write header content-length
write header "X-WebSocket-Version" "wseb-1.1"
write header "X-Sequence-No" "0"
write close

read status "201" /.+/
read version "HTTP/1.1"
read header "X-WebSocket-Version" "wseb-1.1"
read header "Content-Type" "text/plain;charset=utf-8"

read "http://localhost:8080/path/QD12TQhXT6I5KeCrDe6g0IC9qoDkIioh\n"
read "http://localhost:8080/path/T4hpc2vqXueYRyyl3dIQw5K64XLQxuf1\n"
read notify CREATED
read closed

# Downstream
connect http://localhost:8080/path/T4hpc2vqXueYRyyl3dIQw5K64XLQxuf1?.kr=1
connected

write await CREATED
# write parameter ".kb" "256"
write method "GET"
write version "HTTP/1.1"
write header host
write header "X-Sequence-No" "1"
write header content-length
write close

read status "200" /.+/
read header "Content-Type" "application/octet-stream"
read header "Connection" "close"

read [0x80 0x82 0x00]
read ${client256}
read [0x01 0x30 0x31 0xFF]
read notify RECONNECT_RECEIVED
read closed

# Upstream
connect http://localhost:8080/path/QD12TQhXT6I5KeCrDe6g0IC9qoDkIioh
connected

write await CREATED
write method "POST"
write version "HTTP/1.1"
write header host
write header "Content-Type" "application/octet-stream"
write header "X-Sequence-No" "1"
write header content-length

write [0x80 0x82 0x00] ${client256}
write [0x01 0x30 0x31 0xFF]
write close

read status "200" /.+/
read version "HTTP/1.1"
read closed

# Downstream Reconnect
connect http://localhost:8080/path/T4hpc2vqXueYRyyl3dIQw5K64XLQxuf1?.kr=2
connected

write await RECONNECT_RECEIVED
# write parameter ".kb" "256"
write method "GET"
write version "HTTP/1.1"
write header host
write header "X-Sequence-No" "2"
write header content-length
write close

read status "200" /.+/
read header "Content-Type" "application/octet-stream"
read header "Connection" "close"

