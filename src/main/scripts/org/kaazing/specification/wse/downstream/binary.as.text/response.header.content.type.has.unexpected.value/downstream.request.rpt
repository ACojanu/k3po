property sequence ${wse:randomInt(100)}

connect http://localhost:8080/path/;e/ct?query
connected

write method "POST"
write version "HTTP/1.1"
write header host
write header content-length
write header "X-WebSocket-Version" "wseb-1.1"
write header "X-Sequence-No" ${wse:asString(sequence)}
write close

read status "201" /.+/
read version "HTTP/1.1"
read header "X-WebSocket-Version" "wseb-1.1"
read header "Content-Type" "text/plain;charset=utf-8"

read "http://localhost:8080/path/QD12TQhXT6I5KeCrDe6g0IC9qoDkIioh\n"
read "http://localhost:8080/path/T4hpc2vqXueYRyyl3dIQw5K64XLQxuf1\n"
read notify CREATED
read closed

# Downstream
connect http://localhost:8080/path/T4hpc2vqXueYRyyl3dIQw5K64XLQxuf1
connected

write await CREATED
write method "GET"
write version "HTTP/1.1"
write header host
write header "X-Sequence-No" ${wse:asString(sequence + 1)}
write header content-length
write close

read status "200" /.+/
read header "Content-Type" /(?!text\/plain;charset=windows-1252)/
read header "Connection" "close"
read notify DOWNSTREAM_OPENED

# Upstream
connect http://localhost:8080/path/QD12TQhXT6I5KeCrDe6g0IC9qoDkIioh
connected

write await DOWNSTREAM_OPENED
write method "POST"
write version "HTTP/1.1"
write header host
write header "Content-Type" "application/octet-stream"
write header "X-Sequence-No" ${wse:asString(sequence + 1)}
write header content-length

write [0x01 0x30 0x32 0xFF]
write [0x01 0x30 0x31 0xFF]
write close

read status "200" /.+/
read version "HTTP/1.1"
read closed
