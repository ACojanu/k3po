accept http://localhost:8080/path/;e/cte?query
accepted
connected

read method "POST"
read version "HTTP/1.1"
read header "X-WebSocket-Version" "wseb-1.1"
read header "X-Sequence-No" /(?<sequenceNo>\b[0-9]{1,3}\b)/
read closed

write status "201" "Created" 
write version "HTTP/1.1"
write header "X-WebSocket-Version" "wseb-1.1"
write header "Content-Type" "text/plain;charset=utf-8"
write header content-length

write "http://localhost:8080/path/QD12TQhXT6I5KeCrDe6g0IC9qoDkIioh\n"
write "http://localhost:8080/path/T4hpc2vqXueYRyyl3dIQw5K64XLQxuf1\n"
write close

# Downstream
accept http://localhost:8080/path/T4hpc2vqXueYRyyl3dIQw5K64XLQxuf1
accepted
connected

read await CREATED
read method "GET"
read version "HTTP/1.1"
read header "X-Sequence-No" ${wse:increment(sequenceNo, 1)}
read closed

write status "200" "OK"
write version "HTTP/1.1"
write header "Content-Type" "not-text/plain;charset=windows-1252"
write header "Connection" "close"
write flush
write notify DOWNSTREAM_OPENED


# Upstream
accept http://localhost:8080/path/QD12TQhXT6I5KeCrDe6g0IC9qoDkIioh
accepted
connected

read await DOWNSTREAM_OPENED
read method "POST"
read version "HTTP/1.1"
read header "Content-Type" "application/octet-stream"
read header "X-Sequence-No" ${wse:increment(sequenceNo, 1)}

read [0x01 0x30 0x32 0xFF]
read [0x01 0x30 0x31 0xFF]
read notify CLOSE_FRAME_RECEIVED
read closed

write status "200" "OK"
write version "HTTP/1.1"
write header content-length
write close
